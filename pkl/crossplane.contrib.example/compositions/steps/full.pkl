amends "@crossplane.contrib/CompositionResponse.pkl"
import "@crossplane.contrib/crossplane.pkl"

import ".../crds/XR.pkl"
import ".../crds/Object.pkl"

import "@k8s/api/core/v1/ConfigMap.pkl"

local request = new crossplane {
  customResourceTemplates = new {
    ["XR"] {
      ["example.crossplane.io/v1"] = XR
    }
    ["Object"] {
      ["kubernetes.crossplane.io/v1alpha2"] = Object
    }
  }
}.Request

local observedCompositeResource: XR? = request.observed.composite.resource as XR?
local extraResource: Object? = request.getExtraResource("ineed", 0)?.resource as Object?

requirements {
  extraResources {
    ["ineed"] {
      apiVersion = Object.apiVersion
      kind = Object.kind
      match {
        matchName = "required"
      }
    }
  }
}

desired {
  composite {
    resource = new XR {
      status {
        someStatus = "pretty status"
      }
    }
  }
  resources {
    ["cm-one"] = new {
      resource = new Object {
        metadata {
          name = "cm-one"
        }
        spec {
          forProvider {
            manifest = new ConfigMap {
              metadata {
                name = "cm-one"
                namespace = "crossplane-system"
              }
              data {
                ["foo"] = observedCompositeResource?.metadata?.name ?? throw("Composite could not find observed composite name")
                ["required"] = extraResource?.metadata?.name ?? "i could not find what I needed..."
              }
            }
          }
        }
      }
      ready = READY_TRUE
    }
  }
}
results {
  new {
    severity = SEVERITY_NORMAL
    message = "welcome"
  }
}
context {
  ["greetings"] = "with <3 from function-pkl"
  when (request.context.containsKey("apiextensions.crossplane.io/environment")) {
    ["apiextensions.crossplane.io/environment"] = request.context.getOrNull("apiextensions.crossplane.io/environment")
  }
}

meta = if (request.meta != null) new {
  ttl = 60.s
} else null
