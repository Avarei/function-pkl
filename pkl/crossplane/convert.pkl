import "pkl:yaml"
import "@k8s.contrib/convert.pkl"
// import "@k8s/K8sResource.pkl"
import "crossplane:crds"
/*
input = """
  context:
    foo: bar
  observed:
    composite:
      resource:
        apiVersion: example.crossplane.io/v1
        kind: XR
        metadata:
          name: example-xr
        spec: {}
    resources:
      example:
        resource:
          apiVersion: v1
          kind: ConfigMap
          metadata:
            name: example
            namespace: default
          data:
            some: configuration
        connectionDetails:
          foo: bar
        ready: false
  """*/
input: String = read("crossplane:input").text
package: String = read("crossplane:package").text

local parsedInput: Mapping = new yaml.Parser { useMapping = true }
    .parse(input) as Mapping

local importInfos = importsByKindAndVersion.values
  .flatMap((it) -> it.values)
  .sortWith((info1, info2) -> info1.uri < info2.uri)

local o: Listing = new {
  "amends \"\(package)#/CompositionInput.pkl\"\n"
  //"import \"@k8s/api/core/v1/ConfigMap.pkl\"\n" // example
  for (importInfo in importInfos) {
    if (importInfo.isAliased)
      "import \"\(importInfo.uri)\" as \(importInfo.name)\n"
    else
      "import \"\(importInfo.uri)\"\n"
  }
  "\n"
  // TODO add imports for all CRDs used

  if (parsedInput.containsKey("context"))
    "context = \(parsedInput.getOrNull("context"))\n"
  else ""

  if (parsedInput.containsKey("observed") && !parsedInput["observed"].isEmpty)
    """
    observed {
    \(parsedInput.getOrNull("observed").ifNonNull((state) -> indent(getState(state), 2)))
    }\n
    """
  else ""

  if (parsedInput.containsKey("desired") && !parsedInput["desired"].isEmpty)
    """
    desired {
    \(parsedInput.getOrNull("desired").ifNonNull((state) -> indent(getState(state), 2)))
    }\n
    """
  else ""

  if (parsedInput.containsKey("extra_resources"))
    """
    extraResources {
    \(parsedInput.getOrNull("extra_resources").ifNonNull((resources) -> indent(getResources(resources), 2)))
    }\n
    """
  else ""
}

local function indent(text: String, i: Int): String =
  let (spaces: String = " ".repeat(i))
    spaces + text.split("\n").join("\n" + spaces)

function getResources(i: Mapping): String = new Listing {
  /*
  ["required"] {
    items {
      new {
        resource = (ConfigMap) {
          metadata {
            name = "foo"
          }
        }
      }
    }
  }
  */
  for (k, v in i) {
    "[\"\(k)\"] {\n"
    let (items = v.getOrNull("items"))
      if (items != null )
        indent("""
        items {
        \(indent(getResources2(items), 2))
        }\n
        """, 2)
      else ""
    "}\n"
  }
  "\n"
}.join("")

local function getResources2(i: Listing): String = new Listing<String> {
  /*
  new {
    resource = (ConfigMap) {
      metadata {
        name = "foo"
      }
    }
  }
  */
  for (resource in i) {
    "new {\n"
    indent(getResource(resource), 2) + "\n"
    "}\n"
  }
}.join("")

function getState(i: Mapping): String = new Listing {
  "composite {\n"
  indent(getResource(i["composite"]), 2) + "\n"
  "}\n"
  when (i.containsKey("resources")) {
    "resources {\n"
    for (k, v in i["resources"]) {
      indent("""
        ["\(k)"] {
        \(indent(getResource(v), 2))
        }
        """, 2) + "\n"
    }
    "}\n"
  }
}.join("")

function getResource(i: Mapping): String = new Listing<String> {
  let (resource = i.getOrNull("resource"))
    if (resource != null)
      /*"""
      resource = new ConfigMap {
        // TODO add k8s.contrib/convert.pkl here
        metadata {
          name = "foo"
        }
      }\n
      """*/
      """
      resource = new \(resource["kind"])
      \(indent(getK8sResource(resource), 2))
      \n
      """
    else ""

  let (connectionDetails = i.getOrNull("connectionDetails"))
    if (connectionDetails != null)
      """
      connectionDetails {
      \(indent(getConnectionDetails(connectionDetails), 2))
      }\n
      """
    else ""

  let (ready = i.getOrNull("ready"))
    if (ready != null)
      "ready = \(i["ready"])\n"
    else ""
}.toList().join("")

// copied from k8s.contrib@1.0.1/convert.pkl as it is local there.
local function renderConvertedValue(value: Any) =
  new PcfRenderer {}.renderValue(value).replaceAll("_____default_____", "default")

local k8sConverter = new convert {
  customResourceTemplates = crds.resourceTemplates
}

function getK8sResource(i: Mapping): String =
  renderConvertedValue(k8sConverter.resourceConverterFn(i))

local states: List<Mapping> = parsedInput.fold(List(), (res, key, value) ->
  if ((key == "observed" || key == "desired") && !value.isEmpty)
    res.add(value)
  else
    res
) as List<Mapping>

local extraResources: Mapping? = parsedInput.getOrNull("extra_resources")

local k8sResources: Listing<Mapping> = new Listing<Mapping> {
  for (state in states) {
    state["composite"]["resource"]
    when (state.containsKey("resources") == true) {
      for (_, resource in state.getOrNull("resources")) {
        resource["resource"]
      }
    }
  }
  when (extraResources != null) {
    for (_, v in extraResources) {
      for (item in v.getOrNull("items")) {
        item["resource"]
      }
    }
  }
}

// copied/adapted from k8s.contrib@1.0.1/convert.pkl as it is local there.
local importsByKindAndVersion: Map<String, Map<String, ImportInfo>> =
  k8sResources.fold(Map(), (imports, res) ->
    let (importsByVersion = imports.getOrNull(res["kind"]))
      if (importsByVersion == null)
        imports.put(
          res["kind"],
          Map(
            res["apiVersion"],
            new ImportInfo {
              resource = res
              name = res["kind"]
              uri = k8sConverter.getResourceTemplateUri(res)
              isAliased = false
            }))
      else if (importsByVersion.containsKey(res["apiVersion"]))
        imports
      else if (importsByVersion.length == 1)
        let (first = importsByVersion.values.single.resource)
          imports.put(
            res["kind"],
            Map(
              // for consistency, also used aliased name for first import of this kind
              first["apiVersion"], createAliasedInfo(first),
              res["apiVersion"], createAliasedInfo(res)))
      else
        imports.put(
          res["kind"],
          importsByVersion
            .put(res["apiVersion"], createAliasedInfo(res))))

// copied/adapted from k8s.contrib@1.0.1/convert.pkl as it is local there.
local function createAliasedInfo(res: Mapping): ImportInfo = new {
  resource = res
  name = "\(res["kind"])\(res["apiVersion"].split("/").last.capitalize())"
  uri = k8sConverter.getResourceTemplateUri(res)
  isAliased = true
}

// copied from k8s.contrib@1.0.1/convert.pkl as it is local there.
local class ImportInfo {
  resource: Mapping
  name: String
  uri: String
  isAliased: Boolean
}

function getConnectionDetails(i: Mapping<String, String>): String = new Listing {
  for (k, v in i) {
    "[\"\(k)\"] = \"\(v)\"\n"
  }
}.join("")

output {
  text = o.join("")
}
